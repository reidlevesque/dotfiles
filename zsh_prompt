#! /bin/zsh

##### run `spectrum_ls` to see all colours #####
function iTermSetPromptHeader {
  local curcontext=""
  local curkube=""
  local curnamespace=""
  local curdir=""
  local curdatetime=""
  local dispkube=""
  local k8s_prompt_colour=""
  local cloud_details=""

  PROMPT_PATH_COLOUR=""
  PROMPT_SYMBOL_COLOUR=""

  DEV_VM_PROMPT=""
  PYENV_PROMPT=""
  KUBERNETES_PROMPT=""
  CLOUD_PROMPT=""
  DATE_TIME_PROMPT=""

  local light_colour=$FG[008]
  local prompt_separator="${light_colour}-----%{$reset_color%}"

  setPromptColours
  setDevVMPrompt
  setTerraformPrompt
  setPyenvPrompt
  setKubernetesPrompt
  setCloudPrompt
# gdate +"%Y-%m-%d %H:%M:%S.%3N"
  # setDateTimePrompt
}

function setDevVMPrompt {
  if [[ $(uname) == "Linux" ]]; then
    DEV_VM_PROMPT="$FG[202]{vm}%{$reset_color%} "
  fi
}

function setPromptColours {
  if [[ $(uname) == "Linux" ]]; then
    PROMPT_PATH_COLOUR=$FG[032]
    PROMPT_SYMBOL_COLOUR=$FG[202]
  else
    PROMPT_PATH_COLOUR=$FG[032]
    PROMPT_SYMBOL_COLOUR=$FG[105]
  fi
}

function setTerraformPrompt {
  local tf_workspace
  tf_workspace=$(terraform workspace show 2>/dev/null)
  if [[ "${tf_workspace}" = "default" ]]; then
    export TF_PROMPT=""
  else
    export TF_PROMPT="$FG[207](${tf_workspace})%{$reset_color%} ${prompt_separator} "
  fi
}

function setPyenvPrompt {
  if [[ -n "${PYENV_VERSION}" ]]; then
    export PYENV_PROMPT="$FG[172][${PYENV_VERSION}]%{$reset_color%} ${prompt_separator} "
  fi
}

function setKubernetesPrompt {
  if [[ -f "${HOME}/.kube/config" ]]; then
    local curcontext="$(cat ${HOME}/.kube/config | grep "current-context" | awk '{print $2}')"
    local curnamespace=$(cat ~/.kube/config | yq ".contexts.[].context | select(.cluster==\"$curcontext\") | .namespace")
    local curkube=$(echo ${curcontext} | perl -pe 's/.*_(.*?)/$1/g' | perl -pe 's/^us-central1-//g')
    local proxy_enabled=""

    # COMMON
    [[ "${curkube}" =~ ".*-c.*" ]] && k8s_prompt_colour=$FG[005]
    # DEV
    [[ "${curkube}" =~ ".*-d-.*" ]] && k8s_prompt_colour=$FG[002]
    [[ "${curkube}" =~ ".*-x-.*" ]] && k8s_prompt_colour=$FG[002]
    # STAGING
    [[ "${curkube}" =~ ".*-s-.*" ]] && k8s_prompt_colour=$FG[003]
    # PROD
    [[ "${curkube}" =~ ".*-p-.*" ]] && k8s_prompt_colour=$FG[001]

    if [[ -n "${HTTPS_PROXY}" ]]; then
      proxy_enabled="{*} "
    fi

    export KUBERNETES_PROMPT="${k8s_prompt_colour}${proxy_enabled}${curkube} | ${curnamespace}%{$reset_color%} ${prompt_separator} "
  fi
}

function setCloudPrompt {
  if command -v gcloud &> /dev/null; then
    # This is slow (~300ms)
    # local gcloud_profile=$(gcloud config configurations list --filter='is_active:True' --format json | jq -r '.[].name')
    local gcloud_profile="default"
    local gcloud_project=$(cat "${HOME}/.config/gcloud/configurations/config_${gcloud_profile}" | grep project | awk '{print $3}')
    # local gcloud_project=$(gcloud config list --format json | jq -r '.core.project')
    # local gcloud_project=$(gcloud config get-value project)
    # local cloud_details="[${gcloud_profile}] ${gcloud_project}"
    local cloud_details="${gcloud_project}"
    if [[ -n "${cloud_details}" ]]; then
      export CLOUD_PROMPT="${light_colour}${cloud_details}%{$reset_color%} ${prompt_separator} "
    fi
  fi
}

function setDateTimePrompt {
  curdatetime=$(date -u '+%Y-%m-%d %H:%M:%S')
  export DATE_TIME_PROMPT="${light_colour}${curdatetime}%{$reset_color%} ${prompt_separator} "
}

function iTermSetTitle {
	local curproject=""
  local dev_vm=""
	local curdir=$(print -P %~)

	if [[ $curdir = "~/dev/"* ]]
	then
		curproject=`echo $curdir/ | perl -p -e 's#^~/dev/(\S+?)/.*$#$1#g'`
	elif [[ $curdir = "~/groq/"* ]]
	then
		curproject=`echo $curdir/ | perl -p -e 's#^~/groq/(\S+?)/.*$#$1#g'`
	else
		curproject=${curdir: -12}
	fi

  if [[ $(uname) == "Linux" ]]; then
    dev_vm="{vm} "
  fi

	echo -n -e "\e]1;${dev_vm}${curproject}\a"
}

function setprompt {
  export PROMPT='${TF_PROMPT}${PYENV_PROMPT}${KUBERNETES_PROMPT}${CLOUD_PROMPT}${DATE_TIME_PROMPT}
${DEV_VM_PROMPT}${PROMPT_PATH_COLOUR}%~\
$(git_prompt_info) \
%(?..%B[%?%\]%b )\
${PROMPT_SYMBOL_COLOUR}%(!.#.»)%{$reset_color%} '
  export RPROMPT=""
}

function shortprompt {
  export PROMPT='${TF_PROMPT}${PYENV_PROMPT}${KUBERNETES_PROMPT}${CLOUD_PROMPT}${DATE_TIME_PROMPT}
${DEV_VM_PROMPT}${PROMPT_PATH_COLOUR}$(shrink_path -l -t -s -g -4)\
$(git_prompt_info) \
%(?..%B[%?%\]%b )\
${PROMPT_SYMBOL_COLOUR}%(!.#.»)%{$reset_color%} '
  export RPROMPT=""
}

precmd() {
    iTermSetPromptHeader
    iTermSetTitle
}

setprompt
